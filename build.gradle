/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Groovy project to get you started.
 * For more details take a look at the Groovy Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.5-20170301121954+0000/userguide/tutorial_groovy_projects.html
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
        classpath 'org.ajoberstar:gradle-git:1.7.1'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'org.ajoberstar.github-pages' version '1.7.1'
}

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'codenarc'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.9'

    // for generic logging rather than system out
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.4'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.4'
    compile group: 'org.ow2.asm', name: 'asm', version: '5.0.3'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

// Define the main class for the application
mainClassName = 'com.strater.jenn.App'

codenarc {
    toolVersion = '0.27.0'
}
codenarcMain {
    configFile file('gradle/codenarc/codenarc.groovy')
}
codenarcTest {
    configFile file('gradle/codenarc/codenarcTest.groovy')
}

logging.captureStandardOutput LogLevel.INFO

run {
    if (project.hasProperty('files')) {
        args(files.tokenize(','))
    }
}

task newJar(type: ShadowJar) {
    from 'build/bytecode/main'
}

asciidoctorj {
    version = '1.5.4'
}

asciidoctor {
    dependsOn test
    sourceDir = file('src/main/docs')
    sources {
        include 'index.adoc'
    }
    backends = ['html','pdf']
    outputDir file('build/docs')
    attributes \
        'sourcedir': file(buildDir.path + "/docs/snippets"),
        'reportdir': file(buildDir.path + "/docs/bytecode/reports"),
        'source-highlighter': 'coderay',
        'toc': '',
        'idprefix': '',
        'idseparator': '-'
}

build.dependsOn asciidoctor

githubPages {
    repoUri = 'git@github.com:jlstrater/groovy-compiler-project.git'
    targetBranch = 'gh-pages'
    pages {
        from files(buildDir.path + "/docs")
    }
}
